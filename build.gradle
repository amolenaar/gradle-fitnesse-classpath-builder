/*
 * Example build file
 */

apply plugin: 'java'

repositories {
  mavenCentral()
}

configurations {
  fitnesse
}

dependencies {
  fitnesse 'org.fitnesse:fitnesse:20140623'
  runtime 'org.fitnesse:fitnesse:20140623:standalone'
}

task("writeFitNesseClasspath", type: WriteFitNesseClasspath) {
  classpath = project.configurations.fitnesse + configurations.runtime

  pagePath = project.file("FitNesseRoot/GradleClasspath")
}

project.tasks.getByName("clean").dependsOn("cleanWriteFitNesseClasspath")

task("wiki", type: JavaExec) {
  dependsOn writeFitNesseClasspath
  dependsOn compileJava
  classpath configurations.fitnesse
  main "fitnesseMain.FitNesseMain"
  args "-p", "8000", "-e", "0"
}

/// === Custom Tasks

class WriteFitNesseClasspath extends DefaultTask {
  @Input
  def classpath

  @OutputDirectory
  File pagePath

  @TaskAction
  def generatePage() {
    def contentTxt = project.file("${pagePath}/content.txt")
    contentTxt.createNewFile()
    contentTxt.withWriter { writer ->
      writer.writeLine("!path ${project.sourceSets.main.output.classesDir}")
      writer.writeLine("!path src/main/resources")
      classpath.each { d ->
        writer.writeLine("!path $d")
      }
    }
  }
}

